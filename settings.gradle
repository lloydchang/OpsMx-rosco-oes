['kork'].each { prj ->
    String propName = "${prj}Composite"
    String projectPath = "../$prj"
    if (settings.ext.has(propName) && Boolean.parseBoolean(settings.ext.get(propName) as String)) {
        includeBuild projectPath
    }
}

enableFeaturePreview("VERSION_ORDERING_V2")

rootProject.name = 'rosco'

include 'rosco-bom', 'rosco-core', 'rosco-web', 'rosco-manifests'

def setBuildFile(project) {
    project.buildFileName = "${project.name}.gradle"
    project.children.each {
        setBuildFile(it)
    }
}

rootProject.children.each {
    setBuildFile(it)
}

if (spinnakerGradleVersion.endsWith('-SNAPSHOT')) {
    pluginManagement {
        repositories {
            mavenLocal()
            mavenCentral()
            gradlePluginPortal()
             maven{
                url "https://nexus.opsmx.net/repository/maven-snapshots/"
                 credentials {
                    username = "NEXUS_USERNAME"
                    password = "NEXUS_PASSWORD"
                         }
            }
        }
    }
}
